cmake_minimum_required(VERSION 3.16)
project(lqtech_ros2_x3plus LANGUAGES CXX)

message("lqtech_ros2_x3plus project source dir: ${CMAKE_CURRENT_SOURCE_DIR}")     

if(CMAKE_CXX_COMPILER_ID MATCHES "(GNU|Clang)")
  add_compile_options(-Wall -Wextra)
endif()

# set the same behavior for windows as it is on linux
set(CMAKE_WINDOWS_EXPORT_ALL_SYMBOLS ON)

######################################## dependencies ########################################
# find dependencies
set(THIS_PACKAGE_INCLUDE_DEPENDS
  hardware_interface
  pluginlib
  rclcpp
  rclcpp_lifecycle
)
# find dependencies
find_package(Protobuf CONFIG REQUIRED)
find_package(gRPC CONFIG REQUIRED)
find_package(absl CONFIG REQUIRED)

find_package(backward_ros REQUIRED)
find_package(ament_cmake REQUIRED)
foreach(Dependency IN ITEMS ${THIS_PACKAGE_INCLUDE_DEPENDS})
  find_package(${Dependency} REQUIRED)
endforeach()


######################################## grpc lib ########################################
set(_PROTOBUF_PROTOC $<TARGET_FILE:protobuf::protoc>)
set(_REFLECTION gRPC::grpc++_reflection)
set(_GRPC_GRPCPP gRPC::grpc++)
set(_PROTOBUF_LIBPROTOBUF protobuf::libprotobuf)
get_target_property(_GRPC_CPP_PLUGIN_EXECUTABLE gRPC::grpc_cpp_plugin LOCATION)

# where is the Proto file
get_filename_component(x3plus_proto_file "grpc/protos/x3plus.proto" ABSOLUTE)
get_filename_component(x3plus_proto_path "${x3plus_proto_file}" PATH)

# Generated sources
set(grpc_dir "${CMAKE_CURRENT_SOURCE_DIR}/grpc/${PROJECT_NAME}")
set(x3plus_proto_hdrs "${grpc_dir}/x3plus.pb.h")
set(x3plus_proto_srcs "${grpc_dir}/x3plus.pb.cc")
set(x3plus_grpc_proto_hdrs "${grpc_dir}/x3plus.grpc.pb.h")
set(x3plus_grpc_proto_srcs "${grpc_dir}/x3plus.grpc.pb.cc")

# the custom command
add_custom_command(
      OUTPUT "${x3plus_proto_hdrs}" "${x3plus_proto_srcs}" "${x3plus_grpc_proto_hdrs}" "${x3plus_grpc_proto_srcs}"  # file names generated by the command
      COMMAND ${_PROTOBUF_PROTOC} # the name of the command
      ARGS --grpc_out "${grpc_dir}" # the directory where the grpc files should be
        --cpp_out "${grpc_dir}" # the directory where the proto files should be
        -I "${x3plus_proto_path}" # where to find other proto files that this proto depends on
        --plugin=protoc-gen-grpc="${_GRPC_CPP_PLUGIN_EXECUTABLE}"
        "${x3plus_proto_file}" # the proto file based on which the custom command generates all the code
      DEPENDS "${x3plus_proto_file}") # make CMake knows the change of the proto file

# define a library to contain all the generated files
add_library(x3plus_grpc_proto SHARED
  ${x3plus_proto_hdrs}
  ${x3plus_proto_srcs}
  ${x3plus_grpc_proto_hdrs}
  ${x3plus_grpc_proto_srcs})
target_link_libraries(x3plus_grpc_proto
  absl::absl_log
  ${_REFLECTION}
  ${_GRPC_GRPCPP}
  ${_PROTOBUF_LIBPROTOBUF})

######################################## hardware lib ########################################
set(hardware_dir "${CMAKE_CURRENT_SOURCE_DIR}/hardware")

# as a sample of grpc client and service
# foreach(_target greeter_client greeter_server)
#   add_executable(${_target} "${hardware_dir}/${_target}.cc")
#   target_include_directories(${_target} PUBLIC "${CMAKE_CURRENT_SOURCE_DIR}/grpc")
#   target_link_libraries(${_target}
#     x3plus_grpc_proto
#     absl::flags_parse
#     absl::absl_log
#     absl::log_initialize
#     absl::log_globals
#     ${_REFLECTION}
#     ${_GRPC_GRPCPP}
#     ${_PROTOBUF_LIBPROTOBUF})
# endforeach()


set(HARDWARE_LIB_NAME "lqtech_x3plus_hardware_lib")
# source file of the lib
add_library(${HARDWARE_LIB_NAME} SHARED hardware/x3plus.cpp)
# include file of the lib
# target_compile_features(${HARDWARE_LIB_NAME} PUBLIC cxx_std_17)
target_include_directories(${HARDWARE_LIB_NAME} PUBLIC
$<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/hardware/include>
$<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/grpc> 
$<INSTALL_INTERFACE:include/${PROJECT_NAME}>
)
target_link_libraries(${HARDWARE_LIB_NAME} PUBLIC
    x3plus_grpc_proto
    absl::flags_parse
    absl::absl_log
    absl::log_initialize
    absl::log_globals
    ${_REFLECTION}
    ${_GRPC_GRPCPP}
    ${_PROTOBUF_LIBPROTOBUF})
# dependencies of the lib
ament_target_dependencies(
  ${HARDWARE_LIB_NAME} PUBLIC
  ${THIS_PACKAGE_INCLUDE_DEPENDS}
)
pluginlib_export_plugin_description_file(hardware_interface ${PROJECT_NAME}.xml)


######################################## install ########################################
# install grpc 
install(
  FILES
    grpc/lqtech_ros2_x3plus/x3plus.grpc.pb.h
    grpc/lqtech_ros2_x3plus/x3plus.pb.h
  DESTINATION include/${PROJECT_NAME}/${PROJECT_NAME} # the first is a project name, the second is a folder name
)

# if DIRECTORY hardware/include and hardware/include is empty, cmake will do nothing
# if DIRECTORY hardware/include/, cmake will copy all the files and folders under hardware/include to the folder nclude/${PROJECT_NAME}
install(
  DIRECTORY hardware/include/
  DESTINATION include/${PROJECT_NAME}
)

# if use 'DIRECTORY hardware/foldername' and 'hardware/foldername' is not empty, cmake will create 'foldername' folder under share/${PROJECT_NAME} 
# and copy every  file and folder under 'hardware/foldername' to share/${PROJECT_NAME}
install(
  DIRECTORY description/launch description/ros2_control description/urdf description/rviz description/meshes
  DESTINATION share/${PROJECT_NAME}
)

install(
  DIRECTORY bringup/launch bringup/config
  DESTINATION share/${PROJECT_NAME}
)

install(TARGETS ${HARDWARE_LIB_NAME} x3plus_grpc_proto
  EXPORT export_${HARDWARE_LIB_NAME}
  ARCHIVE DESTINATION lib
  LIBRARY DESTINATION lib
  RUNTIME DESTINATION bin
)

# if(BUILD_TESTING)
#   find_package(ament_cmake_pytest REQUIRED)
#   ament_add_pytest_test(example_1_urdf_xacro test/test_urdf_xacro.py)
#   ament_add_pytest_test(view_example_1_launch test/test_view_robot_launch.py)
#   ament_add_pytest_test(run_example_1_launch test/test_rrbot_launch.py)
#   ament_add_pytest_test(run_example_1_launch_cli_direct test/test_rrbot_launch_cli_direct.py)
# endif()


## EXPORTS: for ros2
ament_export_targets(export_${HARDWARE_LIB_NAME} HAS_LIBRARY_TARGET)
ament_export_dependencies(${THIS_PACKAGE_INCLUDE_DEPENDS})
ament_package()
